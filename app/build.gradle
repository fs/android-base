apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply from: 'checkstyle/checkstyle.gradle'
apply from: './jacoco.gradle'

apply from: '../deps.gradle'

ext {
    APPLICATION_ID = "com.flatstack.android"
    isCI = "true".equals(System.getenv("CI"))
    commitMessage = 'git log -1 --pretty=%B'.execute().text.trim()
//    STORE_PASSWORD = System.getenv("PROJECT_NAME_STORE_PASSWORD")
//    KEY_ALIAS = System.getenv("PROJECT_NAME_KEY_ALIAS")
//    KEY_PASSWORD = System.getenv("PROJECT_NAME_KEY_PASSWORD")
}

android {
    compileSdkVersion versions.TARGET_SDK_VERSION
    buildToolsVersion versions.BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion versions.MIN_SDK_VERSION
        targetSdkVersion versions.TARGET_SDK_VERSION

        applicationId APPLICATION_ID
        versionCode 1
        versionName '0.9.1'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        com.android.ddmlib.DdmPreferences.setTimeOut(60000)
        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true
    }

    flavorDimensions "environment"

    productFlavors {
        staging {
            buildConfigField "String", "API_URL", "\"https://example-staging.com\""
            applicationIdSuffix ".staging"
            dimension "environment"
        }

        production {
            buildConfigField "String", "API_URL", "\"https://example.com\""
            dimension "environment"
        }
    }

    signingConfigs {
        debug {
            storeFile file("$rootDir/debug.jks")
            storePassword "12345678"
            keyAlias "debug"
            keyPassword "12345678"
        }
//        release {
//            storeFile file("$rootDir/project_name.jks")
//            storePassword STORE_PASSWORD
//            keyAlias KEY_ALIAS
//            keyPassword KEY_PASSWORD
//        }
    }

    buildTypes {
        debug {
            if (isCI) {
                testCoverageEnabled true
            }
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        if (isCI) {
            unitTests.all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries = !isCI
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        textReport true
        textOutput "stdout"
        lintConfig file("$projectDir/lint.xml")
        warningsAsErrors true
    }

    configurations.all {
        resolutionStrategy.force "com.android.support:support-annotations:$versions.support"
    }
}

repositories {
    mavenCentral()
}

apply from: '../test-deps.gradle'

dependencies {
    implementation supportLibs
    implementation rxJavaLibs
    //    implementation retrofitLibs
    //    implementation okHttpLibs

    compileOnly 'org.jetbrains:annotations:13.0'
    implementation 'com.google.code.gson:gson:2.4'
    implementation 'com.github.bumptech.glide:glide:3.7.0' // view injection
    implementation 'com.jakewharton:butterknife:7.0.0'
    testImplementation unitTestLibs
    androidTestImplementation androidTestsLibs
}

// Grant animation permissions to avoid test failure because of ui sync.
task grantAnimationPermissions(type: Exec, dependsOn: ['installStagingDebug', 'installProductionDebug']) {
    group = 'test'
    description = 'Grant permissions for testing.'

    def absolutePath = file('..') // Get project absolute path
    commandLine "$absolutePath/app/set_animation_permissions.sh com.flatstack.android".split(" ")
}

// Source: http://stackoverflow.com/q/29908110/112705
afterEvaluate {
    // When launching individual tests from Android Studio, it seems that only the assemble tasks
    // get called directly, not the install* versions
    tasks.each { task ->
        if (task.name.endsWith('AndroidTest')) {
            task.dependsOn grantAnimationPermissions
        }
    }
}

apply from: "quality.gradle"